"""Definition of remote execution platforms and toolchains.

The remote platform and toolchains are based on the content of
local_config_platform and local_config_cc_toolchains, when runnin Bazel inside
the same docker image as the the remote system expects.
"""

constraint_setting(
    name = "container-image",
)

constraint_value(
    name = "ubuntu-act-22-04",
    constraint_setting = ":container-image",
)

REMOTE_TARGET_CONSTRAINTS = [
    "@platforms//cpu:x86_64",
    "@platforms//os:linux",
]

REMOTE_EXEC_CONSTRAINTS = [
    "@platforms//cpu:x86_64",
    "@platforms//os:linux",
    ":ubuntu-act-22-04",
]

platform(
    name = "ubuntu-act-22-04-platform",
    constraint_values = REMOTE_EXEC_CONSTRAINTS,
    exec_properties = {
        "container-image": "docker://ghcr.io/catthehacker/ubuntu:act-22.04@sha256:5f9c35c25db1d51a8ddaae5c0ba8d3c163c5e9a4a6cc97acd409ac7eae239448",
        "OSFamily": "Linux",
    },
)

toolchain(
    name = "cc-toolchain-k8",
    exec_compatible_with = REMOTE_EXEC_CONSTRAINTS,
    target_compatible_with = REMOTE_TARGET_CONSTRAINTS,
    toolchain = "@remote_config_cc//:cc-compiler-k8",
    toolchain_type = "@rules_cc//cc:toolchain_type",
)

toolchain(
    name = "cc-toolchain-armeabi-v7a",
    exec_compatible_with = REMOTE_EXEC_CONSTRAINTS,
    target_compatible_with = [
        "@platforms//cpu:arm",
        "@platforms//os:android",
    ],
    toolchain = "@remote_config_cc//:cc-compiler-armeabi-v7a",
    toolchain_type = "@rules_cc//cc:toolchain_type",
)

toolchain(
    name = "sh-toolchain",
    exec_compatible_with = REMOTE_EXEC_CONSTRAINTS,
    toolchain = "@remote_config_sh//:local_sh",
    toolchain_type = "@bazel_tools//tools/sh:toolchain_type",
)
